 buildscript {
     repositories {
         jcenter()
     }
     dependencies {
         classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
     }
 }
plugins {
    id 'io.codearte.nexus-staging' version '0.11.0'
	id 'antlr'
	id 'java-gradle-plugin'
	id 'groovy'
}
apply plugin: 'eclipse'
//apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'



repositories {
    jcenter()
}


archivesBaseName = 'sqlunit4j'
group = "com.github.sqlunit4j"
version = "0.3.3"

dependencies {
    // https://mvnrepository.com/artifact/com.github.iounit/iounit
    compile group: 'com.github.iounit', name: 'iounit', version: '0.8.0'
    
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.25'
    
    // https://mvnrepository.com/artifact/de.vandermeer/asciitable
    compile group: 'de.vandermeer', name: 'asciitable', version: '0.3.2'
    
	// https://mvnrepository.com/artifact/org.antlr/antlr4
    //compileOnly group: 'org.antlr', name: 'antlr4', version: '4.7.2'
    antlr("org.antlr:antlr4:4.7.2")
    
	testCompile 'junit:junit:4.12'
    //testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
	testCompile 'org.hsqldb:hsqldb:2.5.0'
    // https://mvnrepository.com/artifact/org.mockito/mockito-all
	testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
	implementation gradleApi()
	implementation localGroovy()
}

// Optional - Generates visitor classes
generateGrammarSource {
    outputDirectory = file("build/generated-src/antlr/main/org/sqlunit4j/lang")
    arguments += ["-visitor", 
        "-package","org.sqlunit4j.lang",
        "org/sqlunit4j/lang/SQLUnit4JLexer.g4", "org/sqlunit4j/lang/SQLUnit4JParser.g4"]
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

modifyPom {
    project {
        name 'SQLUnit4J'
        description 'SQL Unit tests based on IOUnit & JUnit'
        url 'https://github.com/sqlunit4j/sqlunit4j'
        inceptionYear '2018'
        scm {
            url 'https://github.com/sqlunit4j/sqlunit4j'
            connection 'scm:https://github.com/sqlunit4j/sqlunit4j.git'
            developerConnection 'scm:https://github.com/sqlunit4j/sqlunit4j.git'
        }
        licenses {
            license {
                name 'Eclipse Public License'
                url 'http://www.eclipse.org/legal/epl-v10.html'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'ryaneberly'
                name 'ryan eberly'
                email 'ryaneberly@gmail.com'
            }
        }
    }
}

gradlePlugin {
	plugins {
		simplePlugin {
			id = 'com.github.sqlunit4j'
			implementationClass = 'org.sqlunit4j.plugin.SQLUnitPlugin'
			
		}
	}
}